apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "fr.xebia.voxxeddays"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 6
        versionName "1.0.5"
    }

    signingConfigs {
        release {
            storeFile xebiaStoreFile()
            storePassword xebiaStorePassword()
            keyAlias xebiaKeyAlias()
            keyPassword xebiaKeyPassword()
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        incremental false
    }

    productFlavors{
        zurich {
            applicationIdSuffix ".zurich"

            buildConfigField 'String', 'BACKEND_URL', '"http://confcompanion.xebia.fr/api/v1"'
            buildConfigField 'String', 'SLIDO_URL', '"https://ap.sli.do/event/xfb1afkm/ask"'
            buildConfigField 'String', 'VOTE_URL', '"https://voting-vdz.exteso.com/VDZ16"'
            buildConfigField 'int', 'DEVOXX_BE_CONFERENCE_ID', '19'
            buildConfigField 'String', 'TWITTER_KEY', "\"$project.DEVOXX_TWITTER_KEY\""
            buildConfigField 'String', 'TWITTER_SECRET', "\"$project.DEVOXX_TWITTER_SECRET\""
            buildConfigField 'String', 'TWITTER_QUERY', '"@VoxxedZurich"'
        }

        ticino {
            applicationIdSuffix ".ticino"

            buildConfigField 'String', 'BACKEND_URL', '"http://confcompanion.xebia.fr/api/v1"'
            buildConfigField 'String', 'SLIDO_URL', '"https://ap.sli.do/event/xfb1afkm/ask"'
            buildConfigField 'String', 'VOTE_URL', '"https://voting-vdt.exteso.com/VDT16"'
            buildConfigField 'int', 'DEVOXX_BE_CONFERENCE_ID', '20'
            buildConfigField 'String', 'TWITTER_KEY', "\"$project.DEVOXX_TWITTER_KEY\""
            buildConfigField 'String', 'TWITTER_SECRET', "\"$project.DEVOXX_TWITTER_SECRET\""
            buildConfigField 'String', 'TWITTER_QUERY', '"@VoxxedTicino"'


        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-v13:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:design:23.1.0'

    compile 'com.google.android.gms:play-services-wearable:7.3.0'
    compile 'se.emilsjolander:stickylistheaders:2.3.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.fluent.android:fluent-android-bundle:0.1.2'
    compile('com.squareup.retrofit:retrofit:1.9.0')
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'se.emilsjolander:sprinkles:1.3.1'
    compile 'joda-time:joda-time:2.5'
    compile 'com.github.frankiesardo:icepick:2.3.6'
    compile 'fr.baloomba:viewpagerindicator:2.4.2'
    provided 'com.github.frankiesardo:icepick-processor:2.3.6'
    compile 'com.commonsware.cwac:anddown:0.2.4'
    compile 'com.jakewharton.timber:timber:3.0.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'

    compile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
        transitive = true;
    }

    compile('com.twitter.sdk.android:tweet-ui:1.1.0@aar') {
        transitive = true;
    }
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:23.1.0"
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

def xebiaStoreFile() {
    file(System.getProperty("XEBIA_STORE_PATH"))
}

def xebiaStorePassword() {
    System.getProperty("XEBIA_STORE_PWD")
}

def xebiaKeyAlias() {
    System.getProperty("XEBIA_STORE_ALIAS")
}

def xebiaKeyPassword() {
    System.getProperty("XEBIA_STORE_PWD")
}